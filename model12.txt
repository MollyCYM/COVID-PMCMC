"
model dureau {
  obs y

  state S
  state E
  state I
  state R

  input N

  param sigma
  param beta
  param gamma
  param mu
  param tau

  sub parameter {
    sigma ~ truncated_gaussian(0.23, 0.05, lower = 0) //
    gamma ~ truncated_gaussian(0.12460946, 0.2, lower = 0) // gamma is the period, not the rate
    beta ~ truncated_gaussian(0.57586873, 0.3, lower = 0)
    mu ~ truncated_gaussian(0.001, 0.001, lower = 0)
    tau ~ uniform(0,1)
  }

  sub initial {
    S <-999999
    E <- 1
    I <-0
    R <-0
  }

  sub transition(delta = 1) {
    ode(alg = 'RK4(3)', h = 1.0, atoler = 1.0e-3, rtoler = 1.0e-8) {
      dS/dt = -(beta*S*I)/N
      dE/dt = (beta*S*I)/N - sigma*E
      dI/dt = sigma*E - gamma*I - mu*I
      dR/dt = (gamma+mu)*I
    }
  }

  sub observation {
    y ~ log_normal(log(max((sigma*E)/5, 0)), tau)
  }

  sub proposal_parameter {
    sigma ~ truncated_gaussian(sigma, 0.001,lower=0)
    gamma ~ truncated_gaussian(gamma, 0.001, lower=0)
    beta ~ truncated_gaussian(beta, 0.01, lower=0)
    mu ~ truncated_gaussian(mu,0.0001, lower=0)
    tau ~ truncated_gaussian(tau,0.0001, lower=0)
  }
}"
